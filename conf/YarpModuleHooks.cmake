
#########################################################################
##
## Yet Another Device Compiling Monster
##   --paulfitz
##
#########################################################################


IF (NOT COMPILE_DEVICE_LIBRARY)

SET(YARPY_DEV_GEN ${CMAKE_BINARY_DIR}/generated_code)
IF (NOT EXISTS ${YARPY_DEV_GEN})
	FILE(MAKE_DIRECTORY ${YARPY_DEV_GEN})
ENDIF (NOT EXISTS ${YARPY_DEV_GEN})


# Choose whether to merge library source code.
# ---> now always merge.
SET(MERGE_DEVICE_LIBS TRUE)
#SET(MERGE_DEVICE_LIBS ${MERGE_DEVICE_LIBS_INIT} CACHE BOOL "Try to pack device libraries together a bit")

SET(DEVICE_PREFIX "yarpdev_")

SET(COMPILE_DEVICE_LIBRARY TRUE)

SET(YARPY_LIB_FLAG EXCLUDE_FROM_ALL)

IF (NOT YARPY_DEV_LIB_NAME)
  SET(YARPY_DEV_LIB_NAME devices)
ENDIF (NOT YARPY_DEV_LIB_NAME)

SET(YARPY_DEV_LIST)
SET(YARPY_DEV_SRC_LIST)

MACRO(ADD_LIBRARY LIBNAME)
  IF (YARPY_DEVICES)
    SET(MYNAME "${DEVICE_PREFIX}${LIBNAME}")
    SET(ENABLE_${MYNAME} TRUE CACHE BOOL "Enable/disable compilation of ${MYNAME}")
    IF (ENABLE_${MYNAME})
      IF (MERGE_DEVICE_LIBS)
        # this library is not actually needed
        #_ADD_LIBRARY(${MYNAME} ${YARPY_LIB_FLAG} ${ARGN} ${YARPY_DEV_SRC_LIST})
        FOREACH(SRC ${ARGN})
          GET_SOURCE_FILE_PROPERTY(SRC2 ${SRC} LOCATION)
          SET(YARPY_SRC_LIST ${YARPY_SRC_LIST} ${SRC2})
        ENDFOREACH(SRC ${ARGN})
        SET(YARPY_SRC_LIST ${YARPY_SRC_LIST} ${YARPY_DEV_SRC_LIST})
      ELSE (MERGE_DEVICE_LIBS)
        _ADD_LIBRARY(${MYNAME} ${ARGN} ${YARPY_DEV_SRC_LIST})
        SET(YARPY_SRC_LIST)
      ENDIF (MERGE_DEVICE_LIBS)
      SET(YARPY_LIB_LIST ${MYNAME} ${YARPY_LIB_LIST})
    ELSE (ENABLE_${MYNAME})
        SET(YARPY_SRC_LIST)
        SET(YARPY_DEV_LIST)
    ENDIF (ENABLE_${MYNAME})
    SET(YARPY_DEV_SRC_LIST)
  ELSE (YARPY_DEVICES)
    # pass on call without looking at it
    _ADD_LIBRARY(${LIBNAME} ${ARGN})
  ENDIF (YARPY_DEVICES)
ENDMACRO(ADD_LIBRARY LIBNAME)

MACRO(SET_TARGET_PROPERTIES LIBNAME)
  IF (YARPY_DEVICES)
    SET(MYNAME "${DEVICE_PREFIX}${LIBNAME}")
    ###MESSAGE(STATUS "setting properties for ${MYNAME}: ${ARGN}")
    _SET_TARGET_PROPERTIES(${MYNAME} ${ARGN})
  ELSE (YARPY_DEVICES)
    # pass on call without looking at it
    _SET_TARGET_PROPERTIES(${LIBNAME} ${ARGN})
  ENDIF (YARPY_DEVICES)
ENDMACRO(SET_TARGET_PROPERTIES LIBNAME)

MACRO(ADD_EXECUTABLE EXENAME)
  IF (YARPY_DEVICES)
    MESSAGE(STATUS "ignoring executable ${EXENAME}: ${ARGN}")
    #_ADD_EXECUTABLE(${EXENAME} ${YARPY_LIB_FLAG} ${ARGN})
  ELSE (YARPY_DEVICES)
    # pass on call without looking at it
    _ADD_EXECUTABLE(${EXENAME} ${ARGN})
  ENDIF (YARPY_DEVICES)
ENDMACRO(ADD_EXECUTABLE EXENAME)

IF (MERGE_DEVICE_LIBS)
  MACRO(ADD_DEFINITIONS)
    ###MESSAGE(STATUS "adding definitions: ${ARGN}")
    _ADD_DEFINITIONS(${ARGN})
    SET(YARPY_DEF_LIST ${YARPY_XLIB_LIST} ${ARGN})
  ENDMACRO(ADD_DEFINITIONS)
ENDIF (MERGE_DEVICE_LIBS)

MACRO(LINK_LIBRARIES)
  IF (YARPY_DEVICES)
    ###MESSAGE(STATUS "adding libraries: ${ARGN}")
    _LINK_LIBRARIES(${ARGN})
    SET(YARPY_XLIB_LIST ${YARPY_XLIB_LIST} ${ARGN})
  ELSE (YARPY_DEVICES)
    # pass on call without looking at it
    _LINK_LIBRARIES(${ARGN})
  ENDIF (YARPY_DEVICES)
ENDMACRO(LINK_LIBRARIES)

MACRO(ADD_SUBDIRECTORY SUBDIR)
  IF (YARPY_DEVICES)
    _ADD_SUBDIRECTORY(${SUBDIR} ${ARGN})
    GET_DIRECTORY_PROPERTY(LIBS DIRECTORY ${SUBDIR} DEFINITION YARPY_LIB_LIST)
    GET_DIRECTORY_PROPERTY(XLIBS DIRECTORY ${SUBDIR} DEFINITION YARPY_XLIB_LIST)
    GET_DIRECTORY_PROPERTY(SRCS DIRECTORY ${SUBDIR} DEFINITION YARPY_SRC_LIST)
    GET_DIRECTORY_PROPERTY(INCS DIRECTORY ${SUBDIR} INCLUDE_DIRECTORIES)
    GET_DIRECTORY_PROPERTY(LNKS DIRECTORY ${SUBDIR} LINK_DIRECTORIES)
    GET_DIRECTORY_PROPERTY(DEFS DIRECTORY ${SUBDIR} DEFINITION YARPY_DEF_LIST)
    GET_DIRECTORY_PROPERTY(DEVS DIRECTORY ${SUBDIR} DEFINITION YARPY_DEV_LIST)
    SET(YARPY_LIB_LIST0 ${YARPY_LIB_LIST0} ${LIBS})
    SET(YARPY_XLIB_LIST0 ${YARPY_XLIB_LIST0} ${XLIBS})
    SET(YARPY_SRC_LIST0 ${YARPY_SRC_LIST0} ${SRCS})
    SET(YARPY_INC_LIST0 ${YARPY_INC_LIST0} ${INCS})
    SET(YARPY_LNK_LIST0 ${YARPY_LNK_LIST0} ${LNKS})
    SET(YARPY_DEF_LIST0 ${YARPY_DEF_LIST0} ${DEFS})
    SET(YARPY_DEV_LIST0 ${YARPY_DEV_LIST0} ${DEVS})
  ELSE (YARPY_DEVICES)
    # pass on call without looking at it
    _ADD_SUBDIRECTORY(${SUBDIR} ${ARGN})
  ENDIF (YARPY_DEVICES)
ENDMACRO(ADD_SUBDIRECTORY SUBDIR)

MACRO(YARP_PREPARE_DEVICES)
  SET(YARP_CODE_PRE)
  SET(YARP_CODE_POST)
  FOREACH(dev ${YARPY_DEV_LIST0})
    SET(YARP_CODE_PRE "${YARP_CODE_PRE}\nextern void add_${dev}();")
    SET(YARP_CODE_POST "${YARP_CODE_POST}\n    add_${dev}();")
  ENDFOREACH(dev ${YARPY_DEV_LIST0})
  SET(YARP_LIB_NAME ${YARPY_DEV_LIB_NAME})
  CONFIGURE_FILE(${YARP_MODULE_PATH}/yarpdev_lib.cpp.in
     ${YARPY_DEV_GEN}/all_add_yarpdev.cpp @ONLY  IMMEDIATE)
  CONFIGURE_FILE(${YARP_MODULE_PATH}/yarpdev_lib.h.in
     ${CMAKE_BINARY_DIR}/add_${YARP_LIB_NAME}_devices.h @ONLY  IMMEDIATE)

 SET(YARPY_DEV_LIBRARIES)
 IF (MERGE_DEVICE_LIBS)
  INCLUDE_DIRECTORIES(${YARPY_INC_LIST0})
  LINK_DIRECTORIES(${YARPY_LNK_LIST0})
  _ADD_DEFINITIONS(${YARPY_DEF_LIST0})
  _LINK_LIBRARIES(${YARPY_XLIB_LIST0})
  _ADD_LIBRARY(${YARPY_DEV_LIB_NAME} ${YARPY_SRC_LIST0} ${YARPY_DEV_GEN}/all_add_yarpdev.cpp)
   SET(YARPY_DEV_LIBRARIES ${YARPY_DEV_LIB_NAME} ${YARPY_XLIB_LIST0})
 ELSE (MERGE_DEVICE_LIBS)
   _ADD_LIBRARY(${YARPY_DEV_LIB_NAME} ${YARPY_DEV_GEN}/all_add_yarpdev.cpp)
   SET(YARPY_DEV_LIBRARIES ${YARPY_DEV_LIB_NAME} ${YARPY_XLIB_LIST0})
   FOREACH(LIB ${YARPY_LIB_LIST0})
     ###MESSAGE(STATUS "adding --> ${LIB}")
     TARGET_LINK_LIBRARIES(${YARPY_DEV_LIB_NAME} ${LIB})
     SET(YARPY_DEV_LIBRARIES ${YARPY_DEV_LIBRARIES} ${YARPY_DEV_LIB_NAME} ${YARPY_XLIB_LIST0})
   ENDFOREACH(LIB ${YARPY_LIB_LIST0})
 ENDIF (MERGE_DEVICE_LIBS)

 CONFIGURE_FILE(${YARP_MODULE_PATH}/yarpdev_lib_main.cpp.in
     ${YARPY_DEV_GEN}/yarpdev_${YARPY_DEV_LIB_NAME}.cpp @ONLY  IMMEDIATE)
 _ADD_EXECUTABLE(${YARPY_DEV_LIB_NAME}dev ${YARPY_DEV_GEN}/yarpdev_${YARPY_DEV_LIB_NAME}.cpp)
 TARGET_LINK_LIBRARIES(${YARPY_DEV_LIB_NAME}dev ${YARPY_DEV_LIB_NAME})

 SET(YARPY_DEV_DIR ${CMAKE_CURRENT_BINARY_DIR})
 SET(YARPY_OUT_DIR ${CMAKE_CURRENT_BINARY_DIR})
 CONFIGURE_FILE(${YARP_MODULE_PATH}/DEVICEConfig.cmake.in
     ${CMAKE_CURRENT_BINARY_DIR}/${YARPY_DEV_LIB_NAME}Config.cmake @ONLY  IMMEDIATE)
 EXPORT_LIBRARY_DEPENDENCIES(${CMAKE_CURRENT_BINARY_DIR}/${YARPY_DEV_LIB_NAME}Dependencies.cmake)

ENDMACRO(YARP_PREPARE_DEVICES)


MACRO(END_DEVICE_LIBRARY)
  YARP_PREPARE_DEVICES()
  SET(YARPY_DEVICES FALSE)
ENDMACRO(END_DEVICE_LIBRARY)


ENDIF(NOT COMPILE_DEVICE_LIBRARY)

