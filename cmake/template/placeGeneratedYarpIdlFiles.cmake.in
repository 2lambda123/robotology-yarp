# Copyright (C) 2006-2019 Istituto Italiano di Tecnologia (IIT)
# All rights reserved.
#
# This software may be modified and distributed under the terms of the
# BSD-3-Clause license. See the accompanying LICENSE file for details.

# Configured with the following variables:
#
#  - family = @_family@
#  - temp_dir = @_temp_dir@
#  - name = @_name@
#  - output_dir = @_output_dir@
#  - target_name = @_target_name@
#  - cmake_script = @_cmake_script@

set(_headers)
set(_sources)

file(STRINGS "@_temp_dir@/@_name@_index.txt" _index)
foreach(_gen_file ${_index})
  get_filename_component(_type ${_gen_file} EXT)
  get_filename_component(_main ${_gen_file} NAME_WE)
  if(${_type} STREQUAL ".h")
    if(EXISTS "@_temp_dir@/${_gen_file}")
      configure_file("@_temp_dir@/${_gen_file}"
                     "@_output_dir@/include/${_gen_file}"
                     COPYONLY)
      list(APPEND _headers "include/${_gen_file}")
      list(REMOVE_DUPLICATES _headers)
    else()
      message(WARNING "${_gen_file} not found in @_temp_dir@ dir!")
    endif()
    if(EXISTS "@_temp_dir@/${_main}Reply${_type}")
      configure_file("@_temp_dir@/${_main}Reply${_type}"
                     "@_output_dir@/include/${_prefix}${_main}Reply${_type}"
                     COPYONLY)
      list(APPEND _headers "include/${_main}Reply${_type}")
      list(REMOVE_DUPLICATES _headers)
    endif()
  elseif(${_type} STREQUAL ".cpp")
    if(EXISTS "@_temp_dir@/${_gen_file}")
      configure_file("@_temp_dir@/${_gen_file}"
                     "@_output_dir@/src/${_gen_file}"
                     COPYONLY)
      list(APPEND _sources "src/${_gen_file}")
      list(REMOVE_DUPLICATES _sources)
    else()
      message(WARNING "${_gen_file} not found in @_name@ dir!")
    endif()

  else()
    message(WARNING "Filename extension of ${_gen_file} is neither .h nor .cpp")
  endif()
endforeach(_gen_file)


file(WRITE "@_cmake_script@"
"# Copyright (C) 2006-2019 Istituto Italiano di Tecnologia (IIT)
# All rights reserved.
#
# This software may be modified and distributed under the terms of the
# BSD-3-Clause license. See the accompanying LICENSE file for details.

## This is an automatically-generated file.
## It could get re-generated if the ALLOW_IDL_GENERATION flag is on

set(headers ${_headers})
set(sources ${_sources})
")
